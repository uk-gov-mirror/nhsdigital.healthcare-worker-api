resource "aws_iam_role" "codebuild_deploy_job_role" {
  name = "CodeBuildDeployJobRole"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "codebuild.amazonaws.com"
        }
      },
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          AWS = ["209479271736", "535002889321", "711387117641", "266735814611"]
        }
      },
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          AWS = "arn:aws:iam::209479271736:role/DeploymentPipelineRole"
        }
      }
    ]
  })
}

resource "aws_iam_policy" "fetch_build_artifacts" {
  name = "fetch-build-artifacts"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        "Effect" : "Allow",
        "Action" : "s3:Get*",
        "Resource" : ["arn:aws:s3:::nhse-iam-hcw-build-artifacts-mgmt/*"]
      },
      {
        "Effect" : "Allow",
        "Action" : "s3:ListBucket",
        "Resource" : ["arn:aws:s3:::nhse-iam-hcw-build-artifacts-mgmt"]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "kms:DescribeKey",
          "kms:GenerateDataKey*",
          "kms:Encrypt",
          "kms:ReEncrypt*",
          "kms:Decrypt"
        ],
        "Resource" : [
          "arn:aws:kms:eu-west-2:209479271736:key/866deb7e-6dd0-4c7a-b479-3879651af711"
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : "dynamodb:*",
        "Resource" : "arn:aws:dynamodb:eu-west-2:209479271736:table/terraform-state-lock"
      }
    ]
  })
}

# The deployment job needs a lot of permissions because it's running terraform, which could be modifying lots of different resources
# TODO: Think about if we want to be more restrictive, probably okay with restrictions on branch pushes - raised as https://nhsd-jira.digital.nhs.uk/browse/HCW-103
data "aws_iam_policy" "power_user_policy" {
  arn = "arn:aws:iam::aws:policy/AdministratorAccess"
}

resource "aws_iam_role_policy_attachment" "codebuild_deploy_job_attach_policy" {
  role       = aws_iam_role.codebuild_deploy_job_role.name
  policy_arn = data.aws_iam_policy.power_user_policy.arn
}

resource "aws_iam_role_policy_attachment" "build_artifacts_attach_policy" {
  role       = aws_iam_role.codebuild_deploy_job_role.name
  policy_arn = aws_iam_policy.fetch_build_artifacts.arn
}

resource "aws_codebuild_project" "hcw-api-deploy" {
  name         = "hcw-api-deploy"
  service_role = aws_iam_role.codebuild_deploy_job_role.arn

  environment {
    compute_type = "BUILD_GENERAL1_SMALL"
    image        = "aws/codebuild/amazonlinux2-x86_64-standard:5.0"
    type         = "LINUX_CONTAINER"
  }

  artifacts {
    type = "NO_ARTIFACTS"
  }

  source {
    type      = "GITHUB"
    location  = "https://github.com/NHSDigital/healthcare-worker-api"
    buildspec = "buildspecs/deploy.yml"

    git_submodules_config {
      fetch_submodules = false
    }
  }
}

resource "aws_codebuild_project" "hcw-api-destroy-pr-env" {
  name         = "hcw-api-destroy-pr-env"
  service_role = aws_iam_role.codebuild_deploy_job_role.arn

  environment {
    compute_type = "BUILD_GENERAL1_SMALL"
    image        = "aws/codebuild/amazonlinux2-x86_64-standard:5.0"
    type         = "LINUX_CONTAINER"

    environment_variable {
      name  = "apim_private_key_secret_arn"
      value = aws_secretsmanager_secret.apim_account_private_key.arn
      type  = "PLAINTEXT"
    }
  }

  artifacts {
    type = "NO_ARTIFACTS"
  }

  source {
    type      = "GITHUB"
    location  = "https://github.com/NHSDigital/healthcare-worker-api"
    buildspec = "buildspecs/destroy-pr-env.yml"

    git_submodules_config {
      fetch_submodules = false
    }
  }
}

resource "aws_secretsmanager_secret" "apim_account_private_key" {
  name = "apim-deploy-private-key"
}
